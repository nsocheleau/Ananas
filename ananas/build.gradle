apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

group='com.github.nsocheleau'

android {
    ndkVersion = "27.2.12479018"
    namespace = "iamutkarshtiwari.github.io.ananas"

    compileSdk rootProject.ext.compileSdkVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 4
        versionName "1.3.2"
        // This block is different from the one you use to link Gradle to your ndk-build script.
        externalNativeBuild {
            ndkBuild {
                cFlags "-DANDROID_NDK -DDISABLE_IMPORTGL -DANDROID_SUPPORT_FLEXIBLE_PAGE_SIZES=ON"
                abiFilters 'arm64-v8a', 'armeabi-v7a', 'x86', 'x86_64'
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = "17"
    }
    packagingOptions {
        pickFirst 'lib/arm64-v8a/libphotoprocessing.so'
        pickFirst 'lib/armeabi-v7a/libphotoprocessing.so'
        pickFirst 'lib/x86/libphotoprocessing.so'
        pickFirst 'lib/x86_64/libphotoprocessing.so'
        doNotStrip '*/mips/*.so'
    }
    externalNativeBuild {
        ndkBuild {
            path file('jni/Android.mk')
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    testImplementation "junit:junit:${junit_version}"
    testImplementation "org.robolectric:robolectric:${robolectric_version}"
    implementation "androidx.appcompat:appcompat:${androidx_compat}"
    implementation "androidx.recyclerview:recyclerview:1.3.2"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${kotlin_version}"
    implementation "io.github.eltos:simpledialogfragments:${simple_dialog_fragment_version}"
    api "com.vanniktech:android-image-cropper:${cropper_version}"
    implementation "com.google.android.material:material:${google_material_version}"
    implementation "io.reactivex.rxjava2:rxjava:${rxjava_version}"
    implementation "io.reactivex.rxjava2:rxandroid:${rxandroid_version}"
    implementation "androidx.core:core-ktx:${core_ktx_version}"
    implementation "com.android.support.constraint:constraint-layout:${constraint_layout_version}"
}
repositories {
    mavenCentral()
}

apply plugin: 'maven-publish'

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier.set("sources")
}

publishing {
    publications {
        ananas(MavenPublication) {
            groupId 'com.github.nsocheleau'
            artifactId 'ananas'
            version '1.3.2'
            artifact(sourceJar)
            artifact("$buildDir/outputs/aar/ananas-release.aar")
            pom.withXml {
                final dependenciesNode = asNode().appendNode('dependencies')

                ext.addDependency = { Dependency dep, String scope ->
                    if (dep.group == null || dep.version == null || dep.name == null || dep.name == "unspecified")
                        return // ignore invalid dependencies

                    final dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', dep.group)
                    dependencyNode.appendNode('artifactId', dep.name)
                    dependencyNode.appendNode('version', dep.version)
                    dependencyNode.appendNode('scope', scope)

                    if (!dep.transitive) {
                        // If this dependency is transitive, we should force exclude all its dependencies them from the POM
                        final exclusionNode = dependencyNode.appendNode('exclusions').appendNode('exclusion')
                        exclusionNode.appendNode('groupId', '*')
                        exclusionNode.appendNode('artifactId', '*')
                    } else if (!dep.properties.excludeRules.empty) {
                        // Otherwise add specified exclude rules
                        final exclusionNode = dependencyNode.appendNode('exclusions').appendNode('exclusion')
                        dep.properties.excludeRules.each { ExcludeRule rule ->
                            exclusionNode.appendNode('groupId', rule.group ?: '*')
                            exclusionNode.appendNode('artifactId', rule.module ?: '*')
                        }
                    }
                }

                // List all "api" dependencies (for new Gradle) as "compile" dependencies
                configurations.api.getDependencies().each { dep -> addDependency(dep, "compile") }
                // List all "implementation" dependencies (for new Gradle) as "runtime" dependencies
                configurations.implementation.getDependencies().each { dep -> addDependency(dep, "runtime") }
            }
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/nsocheleau/ananas")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
}